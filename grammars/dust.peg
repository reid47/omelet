{
    var util = {};
    util.typeOf = function(obj, noGenerics) {
        var t = ({}).toString.call(obj).match(/\s([a-z|A-Z]+)/)[1];
        if (t !== "Object") {
            if (t === "Array") {
                if (noGenerics) return "Array";
                var t0 = util.typeOf(obj[0]);
                for (var i=0; i<obj.length; i++) {
                    if (t0 !== util.typeOf(obj[i])) {
                        t0 = "Dynamic";
                        break;
                    }
                }
                return "Array<"+t0+">";
            } else {
                return t;
            }
        }
        return obj.constructor.name;
    }
}

Document = contents:Content
    {
        return {
            kind: "Document",
            contents: contents
        }
    }

Content
    = (DustExpression / Doctype / Comment / RawTag / Tag / Text)*

DustExpression
    = DustComment / Key / Section / SpecialCharacter / Indentation

DustComment
    = "{!" c:(!"!}" c:. {return c})* "!}"
    {
        return {
            kind: "Comment",
            value: c.join("")
        }
    }

Indentation
    = "\n" [ ]*
    {
        return {
            kind: "String",
            value: ""
        }
    }

SpecialCharacter
    = "{~" char:("n"/"r"/"s"/"lb"/"rb") "}"
    {
        var output;
        switch (char) {
            case "n": output = "\n"; break;
            case "r": output = "\r"; break;
            case "s": output = " "; break;
            case "lb": output = "{"; break;
            case "rb": output = "}"; break;
        }
        return {
            kind: "String",
            value: output
        }
    }

Identifier
    = id:([a-zA-Z] [a-zA-Z0-9_-]*)
    {
        return {
            kind: "Identifier",
            value: id[0].concat(id[1].join(""))
        }
    }

Filter
    = "|" id:Identifier
    {
        return {
            kind: "Filter",
            name: id,
            arguments: []
        }
    }

Dot
    = "."
    {
        return {
            kind: "Identifier",
            value: "self__"
        }
    }

Key
    = "{" id:(Identifier / Dot) filters:Filter* "}"
    {
        return {
            kind: "Interpolation",
            name: id,
            arguments: [],
            filters: filters
        }
    }

InlineParameterValue
    = InterpolatedStringLiteral / QuotedAttributeValue / Identifier

InterpolatedStringLiteralInterpolation
    = "{" id:Identifier "}" str:[^{\"]*
    {
        return [
            {
                kind: "Interpolation",
                name: id,
                arguments: [],
                filters: []
            },
            {
                kind: "String",
                value: str.join("")
            }
        ]
    }

InterpolatedStringLiteral
    = "\"" str0:[^{\"]*  interps:InterpolatedStringLiteralInterpolation+ "\""
    {
        interps = interps.reduce(function(a,b) {
            return a.concat(b);
        }, [])
        interps.unshift({
            kind: "String",
            value: str0.join("")
        });
        return {
            kind: "Parenthetical",
            inner: interps,
            filters: []
        }
    }

InlineParameter
    = _ name:Identifier "=" value:InlineParameterValue
    {
        return {
            kind: "Assignment",
            leftSide: name,
            rightSide: value
        }
    }

Section
    = SectionWithoutElse / SectionWithElse / SpecialSection

SpecialSection
    = ExistsSection / NoExistsSection / Block / Partial / InlinePartial

Partial
    = "{>" name:(Identifier / InterpolatedStringLiteral / QuotedAttributeValue) "/}"
    {
        return {
            kind: "InternalConditional",
            saved: undefined,
            predicate: function() {
                var defs = collectDefinitions(ast);
                for (var i=0; i<defs.length; i++) {
                    if (defs[i].override === true) {
                        return false;
                    }
                }
                return true;
            },
            thenCase: {
                kind: "Include",
                file: name
            },
            elseCase: {
                kind: "Extend",
                file: name
            }
        }
    }

InlinePartial
    = "{<" openName:Identifier "}" _ content:Content "{/" closeName:Identifier "}"
    {
        if (openName.value !== closeName.value) {
            throw SyntaxError("Missing closing tag for block "+openName.value);
        }
        return {
            kind: "Assignment",
            leftSide: openName,
            rightSide: {
                kind: "Parenthetical",
                inner: content,
                filters: []
            },
            override: true
        }
    }

Block
    = "{+" openName:Identifier "}" _ content:Content "{/" closeName:Identifier "}"
    {
        if (openName.value !== closeName.value) {
            throw SyntaxError("Missing closing tag for block "+openName.value);
        }
        return {
            kind: "Parenthetical",
            inner: [
                {
                    kind: "Assignment",
                    leftSide: openName,
                    rightSide: {
                        kind: "Parenthetical",
                        inner: content,
                        filters: []
                    }
                },
                {
                    kind: "Interpolation",
                    name: openName,
                    arguments: [],
                    filters: []
                }
            ],
            filters: []
        }
    }

ExistsSection
    = "{?" openName:Identifier "}" _ contents:Content elseSection:("{:else}" Content)? "{/" closeName:Identifier "}"
    {
        if (openName.value !== closeName.value) {
            throw SyntaxError("Missing closing tag for section "+openName.value);
        }
        return {
            kind: "IfStatement",
            predicate: {
                kind: "Interpolation",
                name: openName,
                filters: [
                    {
                        kind: "Filter",
                        name: {
                            kind: "Identifier",
                            value: "exists"
                        },
                        arguments: []
                    }
                ]
            },
            thenCase: contents,
            elseCase: elseSection === null ? undefined : elseSection[1]
        }
    }

NoExistsSection
    = "{^" openName:Identifier "}" _ contents:Content elseSection:("{:else}" Content)? "{/" closeName:Identifier "}"
    {
        if (openName.value !== closeName.value) {
            throw SyntaxError("Missing closing tag for section "+openName.value);
        }
        return {
            kind: "IfStatement",
            predicate: {
                kind: "Interpolation",
                name: openName,
                filters: [
                    {
                        kind: "Filter",
                        name: {
                            kind: "Identifier",
                            value: "notexists"
                        },
                        arguments: []
                    }
                ]
            },
            thenCase: contents,
            elseCase: elseSection === null ? undefined : elseSection[1]
        }
    }

SectionWithElse
    = "{#" openName:Identifier inlineParams:InlineParameter* "}" _ contents0:Content "{:else}" forElse:Content "{/" closeName:Identifier "}"
    {
        if (openName.value !== closeName.value) {
            throw SyntaxError("Missing closing tag for section "+openName.value);
        }
        var contents = inlineParams.concat(contents0);
        return {
            kind: "InternalConditional",
            saved: {openName: openName},
            predicate: function() { return __.typeOf(evalExpr(node.saved.openName),true) === "Array" },
            thenCase: {
                kind: "IfStatement",
                predicate: {
                    kind: "Interpolation",
                    name: openName,
                    arguments: [],
                    filters: [
                        {
                            kind: "Filter",
                            name: {
                                kind: "Identifier",
                                value: "defined"
                            },
                            arguments: []
                        }
                    ]
                },
                thenCase: [
                    {
                        kind: "ForEach",
                        iterator: {
                            kind: "Identifier",
                            value: "self__"
                        },
                        data: openName,
                        body: contents,
                        elseCase: forElse
                    }
                ],
                elseCase: forElse
           },
            elseCase: {
                kind: "IfStatement",
                predicate: {
                    kind: "Interpolation",
                    name: openName,
                    arguments: [],
                    filters: [
                        {
                            kind: "Filter",
                            name: {
                                kind: "Identifier",
                                value: "defined"
                            },
                            arguments: []
                        }
                    ]
                },
                thenCase: contents,
                elseCase: forElse === null ? undefined : forElse
            }
        }
    }

SectionWithoutElse
    = "{#" openName:Identifier inlineParams:InlineParameter* "}" _ contents0:Content "{/" closeName:Identifier "}"
    {
        if (openName.value !== closeName.value) {
            throw SyntaxError("Missing closing tag for section "+openName.value);
        }
        var contents = inlineParams.concat(contents0);
        return {
            kind: "InternalConditional",
            saved: {openName: openName},
            predicate: function() { return __.typeOf(evalExpr(node.saved.openName),true) === "Array" },
            thenCase: {
                kind: "ForEach",
                iterator: {
                    kind: "Identifier",
                    value: "self__"
                },
                data: openName,
                body: contents
           },
            elseCase: {
                kind: "IfStatement",
                predicate: {
                    kind: "Interpolation",
                    name: openName,
                    arguments: [],
                    filters: [
                        {
                            kind: "Filter",
                            name: "defined",
                            arguments: []
                        }
                    ]
                },
                thenCase: contents
            }
        }
    }

/* EVERYTHING BELOW THIS LINE IS BASIC HTML */

RawTag
    = ScriptTag / StyleTag

ScriptTag
    = "<script"i _ attrs:(Attribute _)* ">" s:(!"</script>"i s:. {return s})* "</script>"i
    {
        return {
            kind: "Tag",
            name: "script",
            attributes: attrs.map(function(a) { return a[0] }),
            inner: {
                kind: "Raw",
                value: s.join("")
            }
        }
    }

StyleTag
    = "<style"i _ attrs:(Attribute _)* ">" s:(!"</style>"i s:. {return s})* "</script>"i
    {
        return {
            kind: "Tag",
            name: "style",
            attributes: attrs.map(function(a) { return a[0] }),
            inner: {
                kind: "Raw",
                value: s.join("")
            }
        }
    }

Doctype
    = "<!DOCTYPE "i _ doctype:[^\>]+ _ ">"
    {
        return {
            kind: "Doctype",
            value: doctype.join("").trim()
        }
    }

VoidTag
    = "<" tagName:TagName _ attrs:(Attribute _)* "/>"
    {
        return {
            kind: "Tag",
            name: tagName,
            attributes: attrs.map(function(a) { return a[0] }),
            inner: []
        }
    }

Tag
    = VoidTag
    / openTag:OpenTag inner:Content closeTag:CloseTag
    {
        if (openTag.value !== closeTag.value) {
            throw SyntaxError("Missing closing tag for element "+openTag.value);
        }
        return {
            kind: "Tag",
            name: openTag.value,
            attributes: openTag.attributes,
            inner: inner
        }
    }

OpenTag
    = "<" tagName:TagName _ attrs:(Attribute _)* ">"
    {
        return {
            kind: "String",
            value: tagName,
            attributes: attrs.map(function(a) { return a[0] })
        }
    }

CloseTag
    = "</" tagName:TagName _ ">"
    {
        return {
            kind: "String",
            value: tagName
        }
    }

AttributeName
    = attrName:[^ \t\n\r\"\'\>\/\=]+
    {
        return {
            kind: "String",
            value: attrName.join("")
        }
    }

UnquotedAttributeValue
    = attrValue:[^ \t\n\r\"\'\=\<\>\`]+
    {
        return {
            kind: "String",
            value: attrValue.join("")
        }
    }

QuotedAttributeValue
    = attrValue:( "'" [^\']* "'" / '"' [^\"]* '"')
    {
        return {
            kind: "String",
            value: attrValue[1].join("")
        }
    }

EmptyAttribute
    = attrName:AttributeName
    {
        return {
            kind: "Attribute",
            name: attrName,
            value: attrName
        }
    }

UnquotedAttribute
    = attrName:AttributeName _ "=" _ attrValue:UnquotedAttributeValue
    {
        return {
            kind: "Attribute",
            name: attrName,
            value: attrValue
        }
    }

QuotedAttribute
    = attrName:AttributeName _ "=" _ attrValue:QuotedAttributeValue
    {
        return {
            kind: "Attribute",
            name: attrName,
            value: attrValue
        }
    }

Attribute
    = QuotedAttribute / UnquotedAttribute / EmptyAttribute

TagName
    = tagName:([a-zA-Z0-9\_\-\:]*)
    {
        return tagName.join("")
    }

Text
    = text:[^\<\>{]+
    {
        return {
            kind: "String",
            value: text.join("")
        }
    }

Comment
    = "<!--" c:(!"-->" c:. {return c})* "-->"
    {
        return {
            kind: "CommentHTML",
            value: c.join("")
        }
    }

_ "whitespace"
    = [ \t\n\r]*